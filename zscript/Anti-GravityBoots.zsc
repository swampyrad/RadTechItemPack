//Based off Radsuit code from Hideous Destructor
//-------------------------------------------------
// Anti-Gravity Boots
//-------------------------------------------------

const STRIP_GRAVBOOTS=1400;
const ENC_GRAVBOOTS=35;
const HDLD_GRAVBOOTS="AGV";//Anti-GraVity

const GRAVBOOTS_MAXCHARGE=500;

class WornAntiGravBoots:HDDamageHandler{
	int gravboots_charge;

	default{
		+nointeraction;
		+noblockmap;
		-hdpickup.fullcoverage
		//this flag controls whether you can bandage 
		//with it equipped or not
	
		-inventory.isarmor
		inventory.maxamount 1;inventory.amount 1;
		HDDamageHandler.priority 1000;
		HDPickup.wornlayer STRIP_GRAVBOOTS;
		HDPickup.overlaypriority 150;
		tag "$TAG_GRAVBOOTS";
	}
	states{spawn:TNT1 A 0;stop;}

	override void postbeginplay(){
		super.postbeginplay();
		gravboots_charge=GRAVBOOTS_MAXCHARGE;
	}

	override inventory createtossable(int amt){
		let rrr=owner.findinventory("AntiGravBoots");
		if(rrr)owner.useinventory(rrr);else destroy();
		return null;
	}
	override void attachtoowner(actor owner){
		if(!owner.countinv("AntiGravBoots"))
			owner.A_GiveInventory("AntiGravBoots");
			//owner.gravity=HDCONST_GRAVITY/3;
		super.attachtoowner(owner);
	}
	override void DetachFromOwner(){
		owner.A_TakeInventory("AntiGravBoots",1);
		HDArmour.ArmourChangeEffect(owner,10);
		//this triggers the stun effect when removing gear
		//the number seems to control how long the stun lasts

		owner.gravity=HDCONST_GRAVITY;
		super.DetachFromOwner();
	}

	override void DoEffect(){
	//stuff here happens every tic
		if(stamina>0)stamina--;

		let hp=HDPlayerPawn(owner);
        
    //the boots should recharge if you're not moving
		if(hp
		   &&gravboots_charge>0
		   &&(hp.vel.x>1||hp.vel.y>1||!(hp.vel.z==0))
		  ){gravboots_charge--;

      //if the boots have a charge, they'll 
      //lower your gravity
			if(!gravboots_charge<1){
				owner.gravity=HDCONST_GRAVITY/3;
			}
			else owner.gravity=HDCONST_GRAVITY;

		}//if the player is still, the boots will recharge
		else if(gravboots_charge<GRAVBOOTS_MAXCHARGE)
		    gravboots_charge++;
		    
		if(!hp){destroy();return;}
	}

	override double RestrictSpeed(double speedcap){
	//this changes how fast a player can move while wearing this
		return min(speedcap, 3.5);//default is 1.8
	}
	 
	override void DrawHudStuff(
		hdstatusbar sb,
		hdplayerpawn hpl,
		int hdflags,
		int gzflags
	){
		bool am=hdflags&HDSB_AUTOMAP;
		sb.drawimage(
			"AGRVA0",
			am?(11,137):(-120,-2),
			am?sb.DI_TOPLEFT:
			(sb.DI_SCREEN_CENTER_BOTTOM|sb.DI_ITEM_CENTER_BOTTOM)
		);
        
        //draws charge amount on boots sprite
		sb.drawstring(
			sb.pnewsmallfont,sb.formatnumber(gravboots_charge/5),
			am?(14,136):(-128,-10),
			am?(sb.DI_TOPLEFT|sb.DI_TEXT_ALIGN_RIGHT)
			:(sb.DI_SCREEN_CENTER_BOTTOM|sb.DI_TEXT_ALIGN_RIGHT),
			Font.CR_RED,scale:(0.5,0.5)
		);
	}

	void DestroyAntiGravBoots(){
	//if everything works perfectly, this should never happen
		destroy();
		if(owner){
			owner.A_TakeInventory("PowerIronFeet");
			owner.A_StartSound("AntiGravBoots/burst",CHAN_BODY,CHANF_OVERLAP);
		}
	}
}

class AntiGravBoots:HDPickup{
	default{
		//$Category "Gear/Hideous Destructor/Supplies"
		//$Title "anti-gravity boots"
		//$Sprite "AGRVA0"

		inventory.pickupmessage "You got the anti-gravity boots!";
		inventory.pickupsound "weapons/pocket";
		inventory.icon "AGRVA0";
		hdpickup.bulk ENC_GRAVBOOTS;
		tag "$TAG_GRAVBOOTS";
		hdpickup.refid HDLD_GRAVBOOTS;
	}
	
	override string PickupMessage() {
	    String pickupmessage = Stringtable.Localize("$PICKUP_GRAVBOOTS"); 
	    return pickupmessage;
	}

	override void DetachFromOwner(){
		owner.A_TakeInventory("AntiGravBoots");
		owner.A_TakeInventory("WornAntiGravBoots");
		target=owner;
		super.DetachFromOwner();
	}

	override inventory CreateTossable(int amt){
		if(
			amount<2
			&&owner.findinventory("WornAntiGravBoots")
		){
			owner.UseInventory(self);
			return null;
		}
		return super.CreateTossable(amt);
	}

	override bool BeforePockets(actor other){
	//this controls the 'fast-equip' mechanic when
	//holding Use instead of tapping to pick it up
	
		//put on the armour right away
		if(
			other.player
			&&other.player.cmd.buttons&BT_USE
			&&!other.findinventory("WornAntiGravBoots")
		){
			wornlayer=STRIP_GRAVBOOTS;
			bool intervening=!HDPlayerPawn.CheckStrip(other,self,false);
			wornlayer=0;

			if(intervening)return false;

			HDArmour.ArmourChangeEffect(other,60);//default is 120
			other.A_GiveInventory("AntiGravBoots");
			other.A_GiveInventory("WornAntiGravBoots");
			destroy();
			return true;
		}
		return false;
	}
	override void DoEffect(){
		bfitsinbackpack=(amount!=1||!owner||!owner.findinventory("WornAntiGravBoots"));
		super.doeffect();
	}
	states{
	spawn:
		AGRV A 1;
		AGRV A -1{
			if(!target)return;
			HDF.TransferFire(target,self);
		}
	use:
		TNT1 A 0{
			let owrs=wornantigravboots(findinventory("WornAntiGravBoots"));
			if(owrs){
				if(!HDPlayerPawn.CheckStrip(self,owrs))return;
			}else{
				invoker.wornlayer=STRIP_GRAVBOOTS+1;
				if(!HDPlayerPawn.CheckStrip(self,invoker)){
					invoker.wornlayer=0;
					return;
				}
				invoker.wornlayer=0;
			}

			HDArmour.ArmourChangeEffect(self,10);//default is 120
			let onr=HDPlayerPawn(self);
			if(!countinv("WornAntiGravBoots")){
				A_GiveInventory("WornAntiGravBoots");
			}else{
				actor a;int b;
				inventory wrs=findinventory("WornAntiGravBoots");
				[b,a]=A_SpawnItemEx("AntiGravBoots",0,0,height*0.5,0.2,0,2);
				if(a && wrs){
					vel.z+=0.2;
					vel.xy+=(cos(angle),sin(angle))*0.7;
				}
				A_TakeInventory("WornAntiGravBoots");
			}
			player.crouchfactor=min(player.crouchfactor,0.7);
		}fail;
	}
}

class AGBootsSpawn:actor{
	override void postbeginplay(){
		super.postbeginplay();
  		A_SpawnItemEx("AntiGravBoots",random(-5,5),random(-5,5),flags:SXF_NOCHECKPOSITION);
		self.Destroy();
	}
}